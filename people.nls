breed [people person]

people-own [
    person_state
    mask
    target
    moving?
    seated?
    die?
]

to-report roll_dice [ probability ]
  report random-float 1 <= probability
end

to init_person [x y]
    
    (ifelse
      roll_dice infected-portion [
         set shape "infected_person"
         set person_state state_infected
      ]
      roll_dice immune-portion [
         set shape "immune_person"
         set person_state state_immune
      ] [
         set shape "susceptible_person"
         set person_state state_susceptible
      ]
    )
    set mask mask_none
    set xcor x
    set ycor y
    set die? false
    ; TODO maximize the target distance from other people
    set target nobody
    
    if roll_dice p-sitting [
        set seated? true
        set target min-one-of (patches with [ occupied? = false and pcolor = seat_color ]) [ distance myself ]
    ]
   
    if target = nobody [
        set seated? false 
        set target one-of patches with [ occupied? = false and pcolor = floor_color ] 
    ]
    

    set moving? false 
    if target != nobody [
        ask target [
            set occupied? true
        ]
        face target
        set moving? true
    ]

end

to leave_bus
    ask target [
      set occupied? false
    ]
  
    set target min-one-of (patches with [ pcolor = door_color ]) [ distance myself ]
    face target
    set moving? true
    set die? true
end 

to person_tick
    
    if moving? [
        let current patch-at 0 0

        ifelse current = target [

            if die? [
                die
            ]

            set moving? false

            if seated? [
                let current_seat min-one-of seats [ distance myself ]
                set heading [ heading ] of current_seat
                rt random (sitting-people-rotation-spread * 2) - sitting-people-rotation-spread
            ]
            
        ] [
          face target
          fd 1
        ]
    ]
end

to-report state_susceptible
  report 0
end

to-report state_infected
  report 1
end

to-report state_immune
  report 2
end

to-report mask_none
    report 0
end

to-report mask_cloth
    report 1
end

to-report mask_medical
    report 2
end

to-report susceptible_color 
    report [ 0 40 255 ]
end

to-report infected_color
    report [ 255 20 25 ]
end

to-report immune_color
    report [ 255 10 255 ]
end